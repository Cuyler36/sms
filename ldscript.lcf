MEMORY {
    text: origin = 0x80003100
}

SECTIONS {
    GROUP: {
        .init ALIGN(0x20):{}
        /* TODO: should be extab and extabindex */
        extab_ ALIGN(0x20):{}
        extabindex_ ALIGN(0x20):{}
        .text ALIGN(0x20):{}
        .ctors ALIGN(0x20):{}
        .dtors ALIGN(0x20):{}
        .rodata ALIGN(0x20):{}
        .data ALIGN(0x20):{}
        .bss ALIGN(0x20):{}
        .sdata ALIGN(0x20):{}
        .sbss ALIGN(0x20):{}
        .sdata2 ALIGN(0x20):{}
        .stack ALIGN(0x100):{}
    } > text
    _stack_addr = (_f_sdata2 + SIZEOF(.sdata2) + 65536 + 0x7) & ~0x7;
    _stack_end = _f_sdata2 + SIZEOF(.sdata2);
    _db_stack_addr = (_stack_addr + 0x2000);
    _db_stack_end = _stack_addr;
    __ArenaLo = (_db_stack_addr + 0x1f) & ~0x1f;
    __ArenaHi = 0x81700000;
    
    /* Sizes */
    _size_init = SIZEOF(.init);
    _size_extab = SIZEOF(extab_);
    _size_extabindex = SIZEOF(extabindex_);
    _size_text = SIZEOF(.text);
    _size_ctors = SIZEOF(.ctors);
    _size_dtors = SIZEOF(.dtors);
    _size_rodata = SIZEOF(.rodata);
    _size_data = SIZEOF(.data);
    _size_sdata = SIZEOF(.sdata);
    _size_sdata2 = SIZEOF(.sdata2);
    _size_bss = SIZEOF(.bss); /* unlike the other sections, we can use this size symbol as-is. */
    _size_sbss = SIZEOF(.sbss);
}

FORCEFILES {
    __init_cpp_exceptions.o
    Atom.o
    EXIUart.o
    JUTDbPrint.o
    MarioCheckCol.o
    MarioReceiveMsg.o
    NpcSound.o
    ParamInst.o
    uart_console_io.o
}
